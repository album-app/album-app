plugins {
    id 'java'
    id 'maven'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'org.beryx.jlink' version '2.24.0'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://maven.scijava.org/content/groups/public'
    }

    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation platform('org.scijava:pom-scijava:30.0.0')
    annotationProcessor 'org.scijava:scijava-common:2.84.0'
    compile 'org.scijava:scijava-common'
    compile 'org.scijava:scijava-optional'
    compile 'org.apache.commons:commons-lang3'
    compile 'org.apache.commons:commons-exec:1.3'
    compile 'commons-io:commons-io'
    compile 'org.apache.httpcomponents:httpclient'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'org.openjfx:javafx-controls:15.0.1'
    testCompile 'org.junit.jupiter:junit-jupiter:5.5.1'
}

group = 'mdc.ida'
version = '0.1.0-SNAPSHOT'
description = 'HIPS launcher'
sourceCompatibility = '9'
targetCompatibility = '9'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hips'
            from components.java
            pom {
                name = 'hips'
                description = 'A HIPS launcher'
                url = 'http://www.example.com/library'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'frauzufall'
                        name = 'Deborah Schmidt'
                        email = 'mail@frauzufall.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://example.com/my-library.git'
                    developerConnection = 'scm:git:ssh://example.com/my-library.git'
                    url = 'http://example.com/my-library/'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

application {
    mainClassName = 'mdc.ida.hips.Main'
    applicationName = 'hips'
}

configurations.all {
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    // show standard out and standard error of the test JVM(s) on the console
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        testLogging.exceptionFormat = 'full'
    }
    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'HIPS'
//        jvmArgs = ['-Djdk.gtk.version=2'] // required due to a bug in Java: https://github.com/javafxports/openjdk-jfx/issues/175
    }
    jpackage {
        icon = "/src/main/resources/mdc/ida/hips/icons/plugin.png"
    }
}

