plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'org.beryx.jlink' version '2.24.2'
    id("org.javamodularity.moduleplugin") version "1.8.6"
}

repositories {
    mavenLocal()
    maven {
        url = 'https://maven.scijava.org/content/groups/public'
    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation platform('org.scijava:pom-scijava:30.0.0')
    annotationProcessor 'org.scijava:scijava-common:2.84.0'
    implementation 'org.scijava:scijava-common'
    implementation 'org.scijava:scijava-optional'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.apache.commons:commons-exec:1.3'
    implementation 'commons-io:commons-io'
    implementation 'org.apache.httpcomponents:httpclient'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.openjfx:javafx-controls:15.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
}

group = 'mdc.ida'
version = '0.1.0'
description = 'album launcher'
sourceCompatibility = '9'
targetCompatibility = '9'
ext.jpackageInstallerType = project.hasProperty('jpackageInstallerType') ? project.getProperty('jpackageInstallerType') : 'deb'
ext.jpackageIcon = project.hasProperty('jpackageIcon') ? project.getProperty('jpackageIcon') : 'icon.png'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'album-app'
            from components.java
            pom {
                name = 'album-app'
                description = 'The album app'
                url = 'http://www.example.com/library'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'frauzufall'
                        name = 'Deborah Schmidt'
                        email = 'mail@frauzufall.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://example.com/my-library.git'
                    developerConnection = 'scm:git:ssh://example.com/my-library.git'
                    url = 'http://example.com/my-library/'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

application {
    mainClassName = 'mdc.ida.album.Main'
    applicationName = 'album'
    mainModule = 'mdc.ida.album'
}

configurations.all {
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    // show standard out and standard error of the test JVM(s) on the console
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        testLogging.exceptionFormat = 'full'
    }
    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle(event.message )
    }
}

jlink {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'album'
//        jvmArgs = ['-Djdk.gtk.version=2'] // required due to a bug in Java: https://github.com/javafxports/openjdk-jfx/issues/175
    }
    jpackage {
        icon = "src/main/resources/" + jpackageIcon
        installerOptions += ['--app-version', version.replace('-', '.')]
        installerType = jpackageInstallerType
    }
}

sourceSets {
    main {
        output.setResourcesDir(java.outputDir)
    }
}
